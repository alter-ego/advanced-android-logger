buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.1.1'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'maven'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.1"

    defaultConfig {
        minSdkVersion 8
        targetSdkVersion 23
        versionCode 5
        versionName "1.0.5"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            minifyEnabled false
        }
    }

    productFlavors {
        prod {
            buildConfigField "String", "ENV", "\"PROD\""
        }
        dev {
            buildConfigField "String", "ENV", "\"DEV\""
        }
    }

    defaultPublishConfig "devDebug"
    publishNonDefault false

    sourceSets.main {
        java.srcDirs = ['src/main/java',]
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }
}

if (android.productFlavors.size() > 0) {
    android.libraryVariants.all { variant ->
        // Publish a main artifact, otherwise the maven pom is not generated
        if (android.publishNonDefault && variant.name == android.defaultPublishConfig) {
            def bundleTask = tasks["bundle${name.capitalize()}"]
            artifacts {
                archives(bundleTask.archivePath) {
                    classifier null
                    builtBy bundleTask
                }
            }
        }
    }
}

task clearJar(type: Delete) {
    delete 'build/outputs/advancedandroidlogger.jar'
}

task makeJar(type: Copy) {
    from('build/intermediates/bundles/debug/')
    into('../releases/')
    include('classes.jar')
    rename('classes.jar', 'advancedandroidlogger.jar')
}

makeJar.dependsOn(clearJar, build)

uploadArchives {
    repositories.mavenDeployer {
        def deployPath = file(getProperty('aar.deployPath'))
        repository(url: "file://${deployPath.absolutePath}")
        pom.project {
            groupId 'com.alterego'
            artifactId 'advancedandroidlogger'
            version "1.0.5"
        }
    }
}
